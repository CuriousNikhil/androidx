/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("AndroidXUiPlugin")
    id("kotlin-multiplatform")
}

apply plugin: org.jetbrains.kotlin.gradle.targets.js.npm.NpmResolverPlugin

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)
dependencies {
    kotlinPlugin project(":compose:compiler:compiler")
}

kotlin {
    jvm()
    js(IR) {
        browser() {
            testTask {
                testLogging.showStandardStreams = true
                useKarma {
                    useChromeHeadless()
                    useFirefox()
                }
            }
        }
        binaries.executable()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation project(":compose:runtime:runtime")
                implementation(kotlin("stdlib-common"))
            }
        }

        jsMain {
            dependencies {
                //implementation(kotlin("stdlib-js"))
                implementation(npm('css-typed-om', '0.4.0'))
            }
        }

        jsTest {
            dependencies {
                implementation kotlin("test-js")
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin("test-junit")
                implementation("org.seleniumhq.selenium:selenium-java:3.141.59")
            }
        }

        jvmMain {
            dependencies {
                implementation(project(":compose:ui:ui"))
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:material:material"))
            }
        }
    }
}

androidx {
    name = "Compose Web"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.Compose.WEB
    inceptionYear = "2021"
    description = ""
    legacyDisableKotlinStrictApiMode = true
}


task buildAll {
    dependsOn = [build, ":compose:web:integration:build"]
}

task copyIntegrationDistributionForTests(type: Copy) {
    // This Copy task will be removed when we start the http localhost
    doFirst {
        new File(distributionForTestsPath()).delete()
    }

    from "${project(":compose:web:integration").buildDir.toPath().toString()}/distributions"
    into "${distributionForTestsPath()}"
}

task runSeleniumTests {
    dependsOn = [":compose:web:integration:build", copyIntegrationDistributionForTests, jvmTest]
}

tasks.register("publishAll") {
    group = 'publishing'
    description = 'Publishes all Maven publications to the external Maven repository.'
    dependsOn tasks.withType(PublishToMavenRepository).matching {
        if (it.publication.name == "js") {
            println("${it.name} will be triggered")
            return true
        } else if (it.publication.name == "kotlinMultiplatform") {
            println("${it.name} will be triggered")
            return true
        } else {
            return false
        }
    }
}

static OS currentOs() {
    String os = System.getProperty("os.name").toLowerCase()
    if (os.equals("mac os x")) return OS.MacOS
    if (os.contains("win")) return OS.Windows
    if (os.contains("linux")) return OS.Linux
    throw new Exception("Unknown OS name " + os)
}

static String distributionForTestsPath() {
    return "${System.getenv(currentOs() == OS.Windows ? "HOMEPATH" : "HOME")}/compose-web-tmp/dist"
}

enum OS {
    Linux, Windows, MacOS
}